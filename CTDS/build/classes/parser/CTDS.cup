/*
 * Autores: Jaimez Jacinto, Pereyra Orcasitas Nicolas
 * Proyecto: CompiladorCTDS
 * definicion de la gramatica
 */
package parser;


import java_cup.runtime.*;
import ir.ast.*;
import java.util.LinkedList;


/* Preliminaries to set up and use the scanner.  */
//init with {: scanner.init();              :};
//scan with {: return scanner.next_token(); :};


//code

action code {: public static Program ast; :};

parser code {:

     public void syntax_error(Symbol s){
        System.out.println("Sintax error in line: " + s.left +" column: "+ s.right +" symbol:"+s.value);
     }

     public void unrecovered_syntax_error(Symbol s){
        System.out.println("Unrecovered sintax error in line: " + s.left +" column: "+ s.right +" symbol:"+s.value);
     }
     
     public Program getAST(){
        return CUP$parser$actions.ast;
    }
:}

/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
/* Terminals (tokens returned by the scanner).*/

/*LITERALES*/
terminal Integer INT_LITERAL; 
terminal String FLOAT_LITERAL;
/*OPERADORES*/
terminal    PLUS, MINUS, TIMES, DIVIDE, MOD, OR, AND, NOT, EQ, NOTEQ, EQEQ, LT, GT, LTEQ, GTEQ, PLUSEQ, MINUSEQ;
terminal UMINUS;
/*SEPARADORES*/
terminal    SEMI, COMMA, LPAREN, RPAREN, LKEY, RKEY, LBRACKET, RBRACKET, DOT;    
/*PALABRAS RESERVADAS*/
terminal    TYPE_BOOLEAN, BREAK, CLASS, CONTINUE, ELSE, IF, FOR, WHILE, TYPE_FLOAT, TYPE_INT, RETURN, EXTERN;
terminal  VOID;
terminal String TRUE;
terminal String FALSE;
/*IDENTIFICADORES*/
terminal	String IDENTIFIER;


/*operadores*/
non terminal AssignOpType assign_op;
/*literales*/
non terminal Literal   literal;
non terminal IntLiteral   int_literal;
non terminal BoolLiteral  bool_literal;
non terminal FloatLiteral float_literal;


non terminal Program program;
non terminal Declaration decl;
non terminal LinkedList<FieldDeclaration> field_decl;
non terminal LinkedList<Method> method_decl;
non terminal LinkedList<LocationDeclaration> id_def;
non terminal LinkedList<ClassDeclaration> class_decl;
non terminal String id;
non terminal LinkedList<Expression> expr_sequence;
non terminal LinkedList<Parameter> parameters ;
non terminal LinkedList<Statement> statement_decl;
non terminal Type type;
non terminal MethodCall method_call;
non terminal VarLocation location; 
non terminal LocationDeclaration location_decl;
non terminal Method method;
non terminal Expression expr;
non terminal Statement statement;
non terminal Body body;
non terminal Block block ;

/* Precedences */
precedence left OR;
precedence left AND;
precedence nonassoc LT, GT, LTEQ, GTEQ, NOTEQ, EQEQ;
precedence left PLUS, MINUS;   
precedence left TIMES, DIVIDE, MOD;
precedence right UMINUS;
precedence right IF, ELSE;

start with program;

program ::= class_decl:cd {: ast = new Program(cd); :};

class_decl ::= CLASS id:i LKEY decl:d RKEY class_decl:l {: l.add(new ClassDeclaration(i,d)) ;
            RESULT = l; :}
            |CLASS id:i LKEY RKEY class_decl:l {: l.add(new ClassDeclaration(i)) ;
            RESULT = l; :}
            | {: LinkedList<ClassDeclaration> l = new LinkedList<ClassDeclaration>() ;
            RESULT = l; :};
             
decl ::= field_decl:fd {:RESULT = new Declaration(fd,null);:}
            | method_decl:md {:RESULT = new Declaration(null,md);:}
            | field_decl:fd method_decl:md {:RESULT = new Declaration(fd,md);:};

field_decl ::= type:t id_def:id SEMI {: LinkedList<FieldDeclaration> l = new LinkedList<FieldDeclaration>() ;
                                l.add(new FieldDeclaration(t,id) );
				RESULT = l; :}
            | field_decl:fd type:t id_def:id SEMI  {: fd.add(new FieldDeclaration(t,id) );
				RESULT = fd; :};

id_def ::= location_decl:ld {: LinkedList<LocationDeclaration> l = new LinkedList<LocationDeclaration>() ;
                                l.add(ld);
				RESULT = l; :}
            | id_def:l COMMA location_decl:ld {: l.add(ld);
				RESULT = l; :};


location_decl ::= id:i {:RESULT = new LocationDeclaration(i,ileft,iright); :}
            | id:i LBRACKET int_literal:il RBRACKET {:RESULT = new LocationDeclaration(i,il,ileft,iright); :};
    

method_decl ::= method:m {: LinkedList<Method> l = new LinkedList<Method>() ;
                                l.add(m);
				RESULT = l; :} 
            | method_decl:l method:m {: l.add(m);
						RESULT = l ; :};

method ::= type:t id:i LPAREN RPAREN body:b      {:RESULT = new Method(t,i,b);:}               
            |VOID id:i LPAREN RPAREN body:b {:RESULT = new Method(Type.VOID,i,b);:}
            |VOID id:i LPAREN parameters:p RPAREN body:b  {:RESULT = new Method(Type.VOID,i,p,b);:}
            |type:t id:i LPAREN parameters:p RPAREN body:b {:RESULT = new Method(t,i,p,b);:};

parameters ::= type:t id:i {: LinkedList<Parameter> l = new LinkedList<Parameter>() ; l.add(new Parameter(t,i)); RESULT= l;:}
             | parameters:l COMMA type:t id:i {:  l.add(new Parameter(t,i)); RESULT= l;:};


body ::=  LKEY block:b RKEY{:RESULT= new Body(b);:}
            |EXTERN SEMI{:RESULT= new ExternStmt();:};
                                                                         
block ::= statement_decl:sd {: RESULT = new Block(sd);:}
            | field_decl:fd {: RESULT = new Block(fd);:}
            | field_decl:fd statement_decl:sd  {: RESULT = new Block(sd,fd);:}
            | {: RESULT = new Block();:};

type ::= TYPE_INT {:RESULT = Type.INT ; :}
       | TYPE_FLOAT {:RESULT = Type.FLOAT ; :}
       | TYPE_BOOLEAN {:RESULT = Type.BOOL ; :};

statement_decl ::= statement:s {: LinkedList<Statement> l = new LinkedList<Statement>() ;
                                l.add(s);
				RESULT = l; :} 
		| statement_decl:sd statement:s {: sd.add(s);
						RESULT = sd ; :};

statement ::= location:loc assign_op:op expr:e SEMI  {:RESULT =  new AssignStmt(loc,op,e,locleft,locright);:}
            | method_call:m SEMI {:RESULT = new MethodCallStmt(m);:}
            | IF LPAREN expr:cond RPAREN statement:ifBl ELSE statement:elseBl {:RESULT = new IfStmt(cond,ifBl,elseBl);:}
            | IF LPAREN expr:cond RPAREN statement:ifBl {:RESULT = new IfStmt(cond,ifBl);:}
            | FOR id:i EQ expr:e COMMA expr:e2 statement:s {:RESULT = new ForStmt(i,e,e2,s);:}
            | WHILE expr:exp statement:stmt {:RESULT = new WhileStmt(stmt,exp);:}
            | RETURN expr:exp SEMI {:RESULT= new ReturnStmt(exp);:}
            | RETURN SEMI {:RESULT= new ReturnStmt();:}
            | BREAK SEMI  {:RESULT= new BreakStmt();:}
            | CONTINUE SEMI {:RESULT= new ContinueStmt();:}
            | SEMI {:RESULT = null;:}
            | body:b {:RESULT = b;:};


assign_op ::= EQ {: RESULT = AssignOpType.ASSIGN;:}
            | PLUSEQ  {: RESULT = AssignOpType.INCREMENT;:}
            | MINUSEQ  {: RESULT = AssignOpType.DECREMENT;:};

method_call ::= id:i LPAREN RPAREN  {:RESULT = new MethodCall(i,ileft,iright);:}                                      
            | id:i LPAREN expr_sequence:exp RPAREN  {:RESULT = new MethodCall(i,exp,ileft,iright);:}  
            | id:i DOT method_call:mc  {:RESULT = new MethodCall(i,mc,ileft,iright);:} ; 

location ::= id:i {:RESULT = new VarLocation(i,ileft,iright);:}
            |id:i DOT location:l {:RESULT = new VarLocation(i,l,ileft,iright);:}
            |id:i LBRACKET expr:exp RBRACKET {:RESULT = new VarLocation(i,exp,ileft,iright);:};

expr_sequence ::= expr:e {: LinkedList<Expression> l = new LinkedList<Expression>() ;
			l.add(e);
			RESULT = l; :}   
            | expr_sequence:exps COMMA expr:exp {: exps.add(exp); 
                                                RESULT = exps;:};
 
expr ::= location:loc {: RESULT =loc ; :}
            | method_call:mc {: RESULT = mc ; :}
            | literal:l {: RESULT =l ; :} 
            | expr:e PLUS expr:e2 {: RESULT = new BinOpExpr(e,BinOpType.PLUS,e2,eleft,eright) ; :}
            | expr:e MINUS expr:e2 {: RESULT = new BinOpExpr(e,BinOpType.MINUS,e2,eleft,eright) ; :}
            | expr:e TIMES expr:e2 {: RESULT = new BinOpExpr(e,BinOpType.TIMES,e2,eleft,eright) ; :}
            | expr:e MOD expr:e2 {: RESULT = new BinOpExpr(e,BinOpType.MOD,e2,eleft,eright) ; :}
            | expr:e DIVIDE expr:e2 {: RESULT = new BinOpExpr(e,BinOpType.DIVIDE,e2,eleft,eright) ; :}
            | expr:e LT expr:e2 {: RESULT = new BinOpExpr(e,BinOpType.LT,e2,eleft,eright) ; :}
            | expr:e GT expr:e2 {: RESULT = new BinOpExpr(e,BinOpType.GT,e2,eleft,eright) ; :}
            | expr:e LTEQ expr:e2 {: RESULT = new BinOpExpr(e,BinOpType.LTEQ,e2,eleft,eright) ; :}
            | expr:e GTEQ expr:e2 {: RESULT = new BinOpExpr(e,BinOpType.GTEQ,e2,eleft,eright) ; :}
            | expr:e EQEQ expr:e2 {: RESULT = new BinOpExpr(e,BinOpType.EQEQ,e2,eleft,eright) ; :}
            | expr:e NOTEQ expr:e2 {: RESULT = new BinOpExpr(e,BinOpType.NOTEQ,e2,eleft,eright) ; :}
            | expr:e AND expr:e2 {: RESULT = new BinOpExpr(e,BinOpType.AND,e2,eleft,eright) ; :}
            | expr:e OR expr:e2 {: RESULT = new BinOpExpr(e,BinOpType.OR,e2,eleft,eright) ; :}

            | MINUS expr:e {: RESULT = new UnaryOpExpr(UnaryOpType.MINUS,e,eleft,eright) ; :}  %prec UMINUS 
            | NOT expr:e {: RESULT = new UnaryOpExpr(UnaryOpType.NOT,e,eleft,eright) ; :}  %prec UMINUS
            | LPAREN expr:e RPAREN {: RESULT = e; :};


    
literal ::= int_literal:il {: RESULT = il ; :}
            | float_literal:fl {: RESULT = fl ; :}
            | bool_literal:bl {: RESULT = bl ; :};
    
id ::= IDENTIFIER:i {: RESULT = i ;:};

int_literal ::= INT_LITERAL:n {: RESULT = new IntLiteral(n,nleft,nright) ;:};

bool_literal ::= TRUE:t {: RESULT = new BoolLiteral(t,tleft,tright) ;:} 
	| FALSE:f {: RESULT = new BoolLiteral(f,fleft,fright) ;:} ;

float_literal ::= FLOAT_LITERAL:f {:RESULT = new FloatLiteral(f,fleft,fright);:} ;
